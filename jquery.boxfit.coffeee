# BoxFit v1.0 - jQuery Plugin
# (c) 2012 Michi Kono (michikono.com)
# License: http://www.opensource.org/licenses/mit-license.php
# to use: $('#target-div').boxFit()
# Will make the *text* content inside the div (or whatever tag) scale to fit that tag
(($) ->
  $.fn.boxFit = (options) ->

    settings =
      # the number of font size iterations we should step through until we give up
      step_limit: 1000
      # set to false to NOT align middle (vertically)
      align_middle: true
      # set to false this to NOT center the text (horizontally)
      align_center: true
      # set to false to allow the big text to wrap (useful for when you want text to fill a big vertical area)
      multiline: false
      
    $.extend( settings, options );

    unless settings.multiline
      $(this).css('white-space', 'nowrap')

    original_text = $(this).text()
    $(this).html("") # empty out the object
    $(this).prepend($('<span></span>').text(original_text))
    x_padding = parseInt($(this).css('padding-top'), 10) + parseInt($(this).css('padding-bottom'), 10)
    y_padding = parseInt($(this).css('padding-left'), 10) + parseInt($(this).css('padding-right'), 10)
    original_width = $(this).width()
    original_height = $(this).height()
    current_step = 0
    goal_achieved = false

    inner_span = $(this).children().first()

    # first grow things up until it bulges outside of the target
    while inner_span.width() + x_padding < original_width || inner_span.height() + y_padding < original_height
      goal_achieved = true
      if current_step++ > settings.step_limit
        goal_achieved = false
        break
      inner_span.css("font-size", parseInt(inner_span.css("font-size"), 10) + 1)

    # now reverse the sizing logic to move back one step once we've exceeded our target
    if goal_achieved # only run this if we grew the text
      current_step = 0
      while inner_span.width() + x_padding > original_width || inner_span.height() + y_padding > original_height
        break if current_step++ > settings.step_limit
        inner_span.css("font-size", parseInt(inner_span.css("font-size"), 10) - 1)

    if settings.align_middle
      $(this).css('display', 'table')
      inner_span.css('display', 'table-cell')
      inner_span.css('vertical-align', 'middle')

    if settings.align_center
      $(this).css('text-align', 'center')

)( jQuery )

