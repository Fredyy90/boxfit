# BoxFit v1.0 - jQuery Plugin
# (c) 2012 Michi Kono (michikono.com)
# License: http://www.opensource.org/licenses/mit-license.php
# To use: $('#target-div').boxFit()
# Will make the *text* content inside the div (or whatever tag) scale to fit that tag
(($) ->
  $.fn.boxfit = (options) ->
    return $(this) if this.length == 0 # do nothing if we get passed in a blank object
    settings =
    # the number of font size iterations we should step through until we give up
      step_limit: 200
      # set to false to NOT align middle (vertically)
      align_middle: true
      # set to false this to NOT center the text (horizontally)
      align_center: true
      # set to false to allow the big text to wrap (useful for when you want text to fill a big vertical area)
      multiline: false

    $.extend( settings, options );

    unless settings.multiline
      $(this).css('white-space', 'nowrap')

    original_text = $(this).html()
    $(this).html("") # empty out the object

    # take measurements
    original_width = $(this).width()
    original_height = $(this).height()

    if !original_width || !original_height
      console.info('Set static height/width on target DIV before using boxfit!') if window.console?
      $(this).html(original_text)
    else
      if $(original_text).find("span.boxfitted").length is 0
        span = $("<span></span>").addClass("boxfitted").html(original_text)
        $(this).html span
      else
        $(this).html original_text
        span = $(original_text).find('span.boxfitted')[0]

      current_step = 0
      inner_span = span

      if settings.align_middle
        $(this).css "display", "table"
        inner_span.css "display", "table-cell"
        inner_span.css "vertical-align", "middle"
      if settings.align_center
        $(this).css "text-align", "center"
        inner_span.css "text-align", "center"

      # keep growing the target so long as we haven't exceeded the width or height
      while $(this).width() <= original_width  && $(this).height() <= original_height
        if current_step++ > settings.step_limit
          break
        inner_span.css("font-size", parseInt(inner_span.css("font-size"), 10) + 1)
      # go back one step
      inner_span.css("font-size", parseInt(inner_span.css("font-size"), 10) - 1)
      return this
)( jQuery )
